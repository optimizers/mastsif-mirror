***************************
* SET UP THE INITIAL DATA *
***************************

NAME          OPTCTRL6

*   Problem :
*   *********
*   An optimal control problem

*   This problem is a modification of OPTCNTRL.  All bound constraints
*   were removed.  At the solution, the bounds on U1-U9 are active,
*   so a penalty term was added to the objective:
*
*        mu * ||u(i) - active_bound||
*
*   The value of mu (TDP_mu) was chosen to be very large, making the
*   Hessian of the objective ill-conditioned.

*   Source:
*   B. Murtagh and M. Saunders,
*   Mathematical Programming studies 16, pp 84-117,
*   (example 5.11)

*   SIF input: Nick Gould, June 1990.
*              modified by T. Plantagena, December 1992.

*   classification QQR2-AN-V-V

*   useful parameters

*IE T                   10             $-PARAMETER  n =   32
*IE T                   40             $-PARAMETER  n =  122   original value
*IE T                   100            $-PARAMETER  n =  302
*IE T                   400            $-PARAMETER  n = 1202
 IE T                   1500           $-PARAMETER  n = 4502

 IA T-1       T         -1

 IE 0                   0
 IE 1                   1

*   penalty parameter mu

 RE TDP_mu              1.0D+6

VARIABLES

 DO t         0                        T
 X  x(t)
 X  y(t)
 ND

 DO t         0                        T-1
 X  u(t)
 ND

GROUPS

 N  OBJ

 DO t         0                        T-1
 IA t+1       t         1
 XE B(t)      x(t+1)    1.0            x(t)      -1.0
 XE B(t)      y(t)      -0.2
 XE C(t)      y(t+1)    1.0            y(t)      -1.0
 XE C(t)      x(t)      0.004          u(t)      -0.2
 ND

BOUNDS

 DO t         0                        T-1
 XR OPTCTRL6  x(t)
 XR OPTCTRL6  y(t)
 XR OPTCTRL6  u(t)
 ND

 XX OPTCTRL6  x(0)      10.0
 XR OPTCTRL6  x(T)
 XX OPTCTRL6  y(0)      0.0
 XX OPTCTRL6  y(T)      0.0

START POINT

 DO t         1                        T-1
 X  OPTCTRL6  y(t)      -1.0
 ND

ELEMENT TYPE

 EV SQR       X
 EV SQR_TDP   X

ELEMENT USES

 DO t         0                        T
 XT o(t)      SQR
 ZV o(t)      X                        x(t)
 ND

 DO t         1                        T-1
 XT o_TDP(t)  SQR_TDP
 ZV o_TDP(t)  X                        u(t)
 ND

 DO t         0                        T-1
 XT c(t)      SQR
 ZV c(t)      X                        y(t)
 ND

GROUP USES

 DO t         0                        T
 XE OBJ       o(t)      0.5
 ND

 DO t         1                        T-1
 ZE OBJ       o_TDP(t)                 TDP_mu
 ND

 DO t         0                        T-1
 XE C(t)      c(t)      0.01
 ND

OBJECT BOUND

*   Least square problems are bounded below by zero

 LO OPTCRTL6            0.0

*   Solution

*LO SOLTN              2048.00  (T=40)

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      OPTCTRL6

TEMPORARIES

 R  X_TDP

INDIVIDUALS

*   square element.

 T  SQR

 F                      X * X
 G  X                   X + X
 H  X         X         2.0

 T  SQR_TDP
 A  X_TDP               X - 0.2
 F                      X_TDP * X_TDP
 G  X                   X_TDP + X_TDP
 H  X         X         2.0

ENDATA
