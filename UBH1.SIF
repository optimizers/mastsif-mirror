***************************
* SET UP THE INITIAL DATA *
***************************

NAME          UBH1

*   Problem :
*   *********

*   The problem is to minimize the integral of the control magnitude needed
*   to bring a vehicle, from given position and velocity, to the origin with
*   zero velocity in a fixed amount of time.  The controls are the components
*   of the vehicle acceleration.  It is a quadratic linear problem.
*   The discretization uses the trapezoidal rule.

*   The problem is convex.

*   Source: unscaled problem 1 
*   (ODE = 1, CLS = 1, GRD = 1, MET = T, SEED = 0.) in
*   J.T. Betts and W.P. Huffman,
*   "Sparse Nonlinear Programming Test Problems (Release 1.0)",
*   Boeing Computer services, Seattle, July 1993.

*   SIF input: Ph.L. Toint, October 1993.

*   classification QLR2-MN-V-V

*   Number of grid points

*IE N                   10             $-PARAMETER n=90, m=60   original value
*IE N                   100            $-PARAMETER n=900, m=600
 IE N                   1000           $-PARAMETER n=9000, m=6000
*IE N                   2000           $-PARAMETER n=18000, m=12000

*   Initial and final times (guessed)

 RE T0                  0.0
 RE TF                  1000.0

*   Stepsize

 RI RN        N
 R- TTIME     TF                       T0
 R/ K         TTIME                    RN
 RM -K/2      K         -0.5
 RM K/2       K         0.5

*   Useful constants

 IE 0                   0
 IE 1                   1
 IE 2                   2
 IE 3                   3
 IE 4                   4
 IE 5                   5
 IE 6                   6

VARIABLES

*   State variables

 DO I         1                        6
 DO T         0                        N
 X  Y(I,T)
 OD T
 OD I

*   Control variables

 DO I         1                        3
 DO T         0                        N
 X  U(I,T)
 OD T
 OD I

GROUPS

 N  OBJ

*   State constraints

 DO I         1                        3
 IA I+3       I         3
 DO T         1                        N
 IA T-1       T         -1
 XE S(I,T)    Y(I,T)    1.0            Y(I,T-1)  -1.0
 ZE S(I,T)    Y(I+3,T-1)               -K/2
 ZE S(I,T)    Y(I+3,T)                 -K/2
 OD T
 OD I

 DO I         1                        3
 IA I+3       I         3
 DO T         1                        N
 IA T-1       T         -1
 XE S(I+3,T)  Y(I+3,T)  1.0            Y(I+3,T-1)-1.0
 ZE S(I+3,T)  U(I,T-1)                 -K/2
 ZE S(I+3,T)  U(I,T)                   -K/2
 OD T
 OD I

BOUNDS

 FR UBH1      'DEFAULT'

*   Bounded control

 DO I         1                        3
 DO T         0                        N
 XL UBH1      U(I,T)    -1.0
 XU UBH1      U(I,T)    1.0
 OD T
 OD I

*   Initial position and velocity

 XX UBH1      Y(1,0)    1000.0
 XX UBH1      Y(2,0)    1000.0
 XX UBH1      Y(3,0)    1000.0
 XX UBH1      Y(4,0)    -10.0
 XX UBH1      Y(5,0)    10.0
 XX UBH1      Y(6,0)    -10.0

*   Final position and velocity

 DO I         1                        6
 XX UBH1      Y(I,N)    0.0
 OD I

ELEMENT TYPE

 EV SQ        V

ELEMENT USES

 DO T         0                        N
 DO I         1                        3
 XT E(I,T)    SQ
 ZV E(I,T)    V                        U(I,T)
 OD I
 OD T

GROUP USES

 DO T         1                        N
 IA T-1       T         -1
 DO I         1                        3
 ZE OBJ       E(I,T-1)                 K/2
 ZE OBJ       E(I,T)                   K/2
 OD I
 OD T

OBJECT BOUND

 LO UBH1                0.0

*   Solution

*LO SOLTN(10)           1.14735202967
*LO SOLTN(100)          1.11631518169
*LO SOLTN(1000)         1.11598643493
*LO SOLTN(2000)         1.11587382445

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      UBH1

INDIVIDUALS

 T  SQ
 F                      V * V
 G  V                   V + V
 H  V         V         2.0

ENDATA
