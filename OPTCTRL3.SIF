***************************
* SET UP THE INITIAL DATA *
***************************

NAME          OPTCTRL3

*   Problem :
*   *********
*   An optimal control problem

*   Source:
*   B. Murtagh and M. Saunders,
*   Mathematical Programming studies 16, pp 84-117,
*   (example 5.11)
*   This problem has been modified 18 Dec 92 by Todd Plantenga as follows.
*   To eliminate the active bound constraints on U1 to U9,
*   a penalty term is added to the objective:
*
*       mu*||u(i) - bound||
*
*   By trial and error, a value of mu was found to give good
*   agreement between augmented solution points and real ones,
*   while maintaining the same workload on LANCELOT.
*
*   New quantities are labelled with the string "TDP".
*
*   Solutions for:    case of T=10    case of T=40
*     mu = 1E2         545.7324         719.4766
*     mu = 1E3         549.5660        1347.958
*     mu = 1E4         549.9565        1889.521
*     mu = 1E5                         2030.612
*     mu = 1E6                         2048.003
*     mu = 1E7                         2049.788
*     mu = 1E8                         2049.967
*     mu = infinity    549.999986      2049.977124

*   SIF input: Nick Gould, June 1990 and T. Plantenga December 1992.

*   classification QQR2-AN-V-V

*   useful parameters

*IE T                   10             $-PARAMETER  n =   32
*IE T                   40             $-PARAMETER  n =  122   original value
*IE T                   100            $-PARAMETER  n =  302
*IE T                   400            $-PARAMETER  n = 1202
 IE T                   1500           $-PARAMETER  n = 4502

 IA T-1       T         -1

 IE 0                   0
 IE 1                   1

 RE TDP_mu              1.0D+6

VARIABLES

 DO t         0                        T
 X  x(t)
 X  y(t)
 ND

 DO t         0                        T-1
 X  u(t)
 ND

GROUPS

 N  OBJ

 DO t         0                        T-1
 IA t+1       t         1
 XE B(t)      x(t+1)    1.0            x(t)      -1.0
 XE B(t)      y(t)      -0.2
 XE C(t)      y(t+1)    1.0            y(t)      -1.0
 XE C(t)      x(t)      0.004          u(t)      -0.2
 ND

BOUNDS

 DO t         0                        T-1
 XR OPTCTRL3  x(t)
 XR OPTCTRL3  y(t)
 XR OPTCTRL3  u(t)
 ND

 XX OPTCTRL3  x(0)      10.0
 XR OPTCTRL3  x(T)
 XX OPTCTRL3  y(0)      0.0
 XX OPTCTRL3  y(T)      0.0

START POINT

 DO t         1                        T-1
 X  OPTCTRL3  y(t)      -1.0
 ND

ELEMENT TYPE

 EV SQR       X
 EV SQR_TDP   X

ELEMENT USES

 DO t         0                        T
 XT o(t)      SQR
 ZV o(t)      X                        x(t)
 ND

 DO t         1                        T-1
 XT o_TDP(t)  SQR_TDP
 ZV o_TDP(t)  X                        u(t)
 ND

 DO t         0                        T-1
 XT c(t)      SQR
 ZV c(t)      X                        y(t)
 ND

GROUP USES

 DO t         0                        T
 XE OBJ       o(t)      0.5
 ND

 DO t         1                        T-1
 ZE OBJ       o_TDP(t)                 TDP_mu
 ND

 DO t         0                        T-1
 XE C(t)      c(t)      0.01
 ND

OBJECT BOUND

*   Least square problems are bounded below by zero

 LO OPTCNTL             0.0

*   Solution

*LO SOLTN               549.999986  (T=10)
*LO SOLTN              2049.977124  (T=40)

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      OPTCTRL3

TEMPORARIES

 R  X_TDP

INDIVIDUALS

*   square element.

 T  SQR

 F                      X * X
 G  X                   X + X
 H  X         X         2.0

 T  SQR_TDP
 A  X_TDP               X - 0.2
 F                      X_TDP * X_TDP
 G  X                   X_TDP + X_TDP
 H  X         X         2.0

ENDATA
