***************************
* SET UP THE INITIAL DATA *
***************************

NAME          POWELL20

*   Problem :
*   *********

*   A degenerate convex quadratic problem with variable dimension.

*   Formulation:
*   -----------

*                 n    2
*   minimize 1/2 sum  x
*                i=1   i

*   subject to
*                                        k 
*             x     - x    > - 0.5 + (-1)  k   , k = 1, ... , n-1
*              k+1     k   =
*   and 
*             x   - x    > n - 0.5
*              1     n   =


*   Source: a generalization of problem 20 in
*   M. J. D. Powell
*   "On the quadratic progamming algorithm of Goldfarb and Idnani",
*   Mathematical Programmimg Study 25 (1985) 46-61.

*   SIF input: Nick Gould, August 1994.

*   classification QLR2-AN-V-V

*IE N                   10             $-PARAMETER     original value
*IE N                   100            $-PARAMETER
*IE N                   1000           $-PARAMETER
 IE N                   5000           $-PARAMETER
*IE N                   10000          $-PARAMETER

*   Constants

 IE 1                   1

*  Useful parameters

 IA N-1       N         -1
 RI RN        N
 RA N-1/2     RN        -0.5

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 N  OBJ

 DO K         1                        N-1
 IA K+1       K         1
 XG CON(K)    X(K+1)    1.0            X(K)      -1.0
 ND

 XG CON(N)    X(1)      1.0            X(N)      -1.0

CONSTANTS

 RE PLUSMINUS           1.0

 DO K         1                        N-1
 RI RK        K
 RM PLUSMINUS PLUSMINUS -1.0
 R* RHS       PLUSMINUS                RK
 RA RHS       RHS       -0.5 
 Z  POWELL20  CON(K)                   RHS
 ND

 Z  POWELL20  CON(N)                   N-1/2

BOUNDS

 FR POWELL20  'DEFAULT' 

ELEMENT TYPE

 EV SQ        X

ELEMENT USES

*   The elements corresponding to the squre variables

 T  'DEFAULT' SQ
 DO I         1                        N
 ZV XSQ(I)    X                        X(I)
 ND

GROUP USES

*   The diagonal elements

 DO I         1                        N
 XE OBJ       XSQ(I)
 OD I

OBJECT BOUND

*   Solution

*LO SOLTN               

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      POWELL20

INDIVIDUALS

 T  SQ
 F                      5.0D-1 * X * X
 G  X                   X
 H  X         X         1.0D+0

ENDATA


